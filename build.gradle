plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.20'
    id "io.papermc.paperweight.userdev" version "1.5.9"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "org.jlleitschuh.gradle.ktlint" version "11.6.1"
    id 'maven-publish'
}

group = 'io.zkz.mc'
version = '6.0.0-SNAPSHOT'

ext {
    // SDK
    kotlinVersion = '1.9.20'
    targetJavaVersion = 17

    // Libraries
    paperVersion = '1.20.2-R0.1-SNAPSHOT'
    reflectionsVersion = '0.10.2'
    cloudCommandFrameworkVersion = '1.8.4'

    // Plugins
    protocolLibVersion = '5.1.0'

    // MC
    apiVersion = '1.20'
}

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:${paperVersion}")

    implementation "org.reflections:reflections:${reflectionsVersion}"

    implementation "cloud.commandframework:cloud-paper:${cloudCommandFrameworkVersion}"
    implementation "cloud.commandframework:cloud-minecraft-extras:${cloudCommandFrameworkVersion}"

    compileOnly "com.comphenix.protocol:ProtocolLib:${protocolLibVersion}"
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion as String)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = targetJavaVersion
}

processResources {
    def props = [
            version: version,
            name: name,
            apiVersion: apiVersion
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

kotlin {
    jvmToolchain(targetJavaVersion)
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ZekNikZ-Minecraft-Addons/gametools"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}